
plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'it.scoppelletti.spaceship.android-library'
    id 'org.jetbrains.dokka'
}

group 'it.scoppelletti.spaceship'
version '1.1.0'
description 'Spaceship for Preferences'
archivesBaseName = 'spaceship-preference'

android {
    namespace 'it.scoppelletti.spaceship.preference'
    compileSdk sdk_target
    defaultConfig {
        minSdk sdk_min
        targetSdk sdk_target
        vectorDrawables.useSupportLibrary = true
        aarMetadata {
            minCompileSdk = sdk_min
        }
    }
    buildFeatures {
        viewBinding true
    }
    buildTypes {
        release {
            minifyEnabled false
            consumerProguardFiles 'consumer-rules.pro'
        }
        debug {
            minifyEnabled false
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
        freeCompilerArgs += '-Xexplicit-api=strict'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
}

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-preference'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-preference.git'
    inceptionYear = '2018-2021'
}

configurations {
    bom
    api.extendsFrom(bom)
    kapt.extendsFrom(bom)
    coreLibraryDesugaring.extendsFrom(bom)
}

dependencies {
    bom platform(KOTLIN_BOM)
    bom platform(SPACESHIP_BOM)
    api 'androidx.activity:activity-ktx'
    api 'androidx.annotation:annotation'
    api 'androidx.appcompat:appcompat'
    api 'androidx.constraintlayout:constraintlayout'
    api 'androidx.fragment:fragment-ktx'
    api 'androidx.preference:preference-ktx'
    api 'com.google.android.material:material'
    api 'it.scoppelletti.spaceship:spaceship'
    api 'it.scoppelletti.spaceship:spaceship-stdlib'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs'
}
